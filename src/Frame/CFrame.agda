{-# OPTIONS --safe --without-K #-}
open import Frame.IFrame

module Frame.CFrame {W : Set} {_âŠ†_ : W â†’ W â†’ Set} (IF : IFrame W _âŠ†_) where

open IFrame IF

open import Data.Unit using (âŠ¤)
open import Function using (const)

open import Relation.Binary using (IsEquivalence)

open import Relation.Binary.PropositionalEquality using (_â‰¡_)
  renaming (refl to â‰¡-refl ; sym to â‰¡-sym ; trans to â‰¡-trans ; cong to â‰¡-cong
           ; subst to â‰¡-subst ; substâ‚‚ to â‰¡-substâ‚‚)
open import Data.Product using (Î£ ; âˆƒ; âˆƒâ‚‚; _Ã—_; _,_; -,_)
  renaming (projâ‚ to fst; projâ‚‚ to snd)

open import Function using (id ; _âˆ˜_)

open import PUtil using (Î£Ã—-â‰¡,â‰¡,â‰¡â†’â‰¡)

private
  variable
    w w' w'' u u' v v' : W

module Core
  -- Neighborhood function, which assigns a neighborhood to a world
  -- Intuition: think of K having the type W â†’ ğ’« (ğ’« W)
  (K   : W â†’ Set)
  -- Membership relation: v âˆˆ k means v is in the neighborhood k (of w)
  -- Intuition: set membership
  (_âˆˆ_ : (v : W) {w : W} â†’ K w â†’ Set)
  where

  -- a predicate satisfied by all elements of a neighborhood
  ForAllW : (k : K w) (P : W â†’ Set) â†’ Set
  ForAllW k P = âˆ€ {v} â†’ v âˆˆ k â†’ P v

  -- ForAllW flipped
  AllForW : (P : W â†’ Set) (k : K w) â†’ Set
  AllForW P k = ForAllW k P

  -- a predicate satisfied by all proofs witnessing membership
  ForAllâˆˆ : (k : K w) (P : âˆ€ {v} â†’ v âˆˆ k â†’ Set) â†’ Set
  ForAllâˆˆ k P = âˆ€ {v} â†’ (p : v âˆˆ k) â†’ P p

  -- ordering of neighborhoods
  _âŠ†k_ : K w â†’ K w' â†’ Set
  k âŠ†k k' = ForAllW k' (Î» v' â†’ âˆƒ Î» v â†’ v âˆˆ k Ã— (v âŠ† v'))

  âŠ†k-refl[_] : (k : K w) â†’ k âŠ†k k
  âŠ†k-refl[ k ] {v} p = v , p , âŠ†-refl[ v ]

  âŠ†k-trans : {k : K w} {k' : K w'} {k'' : K w''}
    â†’ k âŠ†k k' â†’ k' âŠ†k k'' â†’ k âŠ†k k''
  âŠ†k-trans is is' {v''} p'' = let
    (v' , p' , i') = is' p''
    (v , p , i)    = is p'
    in (v , p , âŠ†-trans i i')

  -- extensional equality of ForAllW proofs
  ForAllWâ‰‹ : (k : K w) {P : W â†’ Set} â†’ (f : ForAllW k P) (g : ForAllW k P) â†’  Set
  ForAllWâ‰‹  {w} k f g = ForAllâˆˆ k Î» p â†’ f p â‰¡ g p

  -- ForAllWâ‰‹ is an equivalence
  module _ {k : K w} {P : W â†’ Set}  where

    ForAllWâ‰‹-refl : (f : ForAllW k P) â†’ ForAllWâ‰‹ k f f
    ForAllWâ‰‹-refl f = Î» _p â†’ â‰¡-refl

    ForAllWâ‰‹-sym : {f f' : ForAllW k P} â†’ ForAllWâ‰‹ k f f' â†’ ForAllWâ‰‹ k f' f
    ForAllWâ‰‹-sym fâ‰¡f' = Î» p â†’ â‰¡-sym (fâ‰¡f' p)

    ForAllWâ‰‹-trans : {f f' f'' : ForAllW k P} â†’ ForAllWâ‰‹ k f f' â†’ ForAllWâ‰‹ k f' f'' â†’ ForAllWâ‰‹ k f f''
    ForAllWâ‰‹-trans fâ‰¡f' f'â‰¡f'' = Î» p â†’ â‰¡-trans (fâ‰¡f' p) (f'â‰¡f'' p)

  -- extensional equality of neighborhood ordering
  module _ {k : K w} {k' : K w'} where

    _â‰‹[âŠ†k]_ : k âŠ†k k' â†’ k âŠ†k k' â†’ Set
    _â‰‹[âŠ†k]_ = ForAllWâ‰‹ k'

    â‰‹[âŠ†k]-refl : (is : k âŠ†k k') â†’ is â‰‹[âŠ†k] is
    â‰‹[âŠ†k]-refl = ForAllWâ‰‹-refl

    â‰‹[âŠ†k]-sym : {is is' : k âŠ†k k'} â†’ is â‰‹[âŠ†k] is' â†’ is' â‰‹[âŠ†k] is
    â‰‹[âŠ†k]-sym = ForAllWâ‰‹-sym

    â‰‹[âŠ†k]-trans : {is is' is'' : k âŠ†k k'} â†’ is â‰‹[âŠ†k] is' â†’ is' â‰‹[âŠ†k] is'' â†’ is â‰‹[âŠ†k] is''
    â‰‹[âŠ†k]-trans = ForAllWâ‰‹-trans

  âŠ†k-trans-unit-left : {k : K w} {k' : K w'} (is : k âŠ†k k')
    â†’ âŠ†k-trans âŠ†k-refl[ k ] is â‰‹[âŠ†k] is
  âŠ†k-trans-unit-left is p = let (_ , _ , i) = is p
    in Î£Ã—-â‰¡,â‰¡,â‰¡â†’â‰¡ (â‰¡-refl , â‰¡-refl , âŠ†-trans-unit-left i)

  âŠ†k-trans-unit-right : {k : K w} {k' : K w'} (is : k âŠ†k k')
    â†’ âŠ†k-trans is âŠ†k-refl[ k' ] â‰‹[âŠ†k] is
  âŠ†k-trans-unit-right is p = let (_ , _ , i) = is p
    in Î£Ã—-â‰¡,â‰¡,â‰¡â†’â‰¡ (â‰¡-refl , â‰¡-refl , âŠ†-trans-unit-right i)

  âŠ†k-trans-assoc : {k : K u} {k' : K v} {k'' : K w} {k''' : K w'}
    â†’ (is : k âŠ†k k') (is' : k' âŠ†k k'') (is'' : k'' âŠ†k k''')
    â†’ âŠ†k-trans (âŠ†k-trans is is') is'' â‰‹[âŠ†k] âŠ†k-trans is (âŠ†k-trans is' is'')
  âŠ†k-trans-assoc is is' is'' p''' = let
    (_ , p'' , i'') = is'' p'''
    (_ , p' , i')   = is' p''
    (_ , _ , i)     = is p'
    in Î£Ã—-â‰¡,â‰¡,â‰¡â†’â‰¡ (â‰¡-refl , â‰¡-refl , âŠ†-trans-assoc i i' i'')

  -- "factorisation of k @ v"
  _ï¼ _ : K w â†’ W â†’ Set
  k ï¼  v = Î£ (K v) Î» k' â†’ k âŠ†k k'

  module _ {k : K w} {w' : W} where

    _â‰‹[ï¼ ]_ : k ï¼  w' â†’ k ï¼  w' â†’ Set
    (k1' , is1) â‰‹[ï¼ ] (k2' , is2) = Î£ (k1' â‰¡ k2') Î» k1â‰¡k2 â†’ â‰¡-subst (_{-k-} âŠ†k_) k1â‰¡k2 is1 â‰‹[âŠ†k] is2

    â‰‹[ï¼ ]-refl : (x : k ï¼  w') â†’ x â‰‹[ï¼ ] x
    â‰‹[ï¼ ]-refl (k , is) = â‰¡-refl {x = k} , â‰‹[âŠ†k]-refl is

    â‰‹[ï¼ ]-sym : {x y : k ï¼  w'} â†’ x â‰‹[ï¼ ] y â†’ y â‰‹[ï¼ ] x
    â‰‹[ï¼ ]-sym (â‰¡-refl , isâ‰‹is') = â‰¡-refl , â‰‹[âŠ†k]-sym isâ‰‹is'

    â‰‹[ï¼ ]-trans : {x y z : k ï¼  w'} â†’ x â‰‹[ï¼ ] y â†’ y â‰‹[ï¼ ] z â†’ x â‰‹[ï¼ ] z
    â‰‹[ï¼ ]-trans (â‰¡-refl , isâ‰‹is') (â‰¡-refl , is'â‰‹is'') = â‰¡-refl , â‰‹[âŠ†k]-trans isâ‰‹is' is'â‰‹is''

  -- factorising function
  _â‡’k_ : W â†’ W â†’ Set
  w â‡’k v = (k : K w) â†’ k ï¼  v

  -- restriction of a factorisation function
  -- to the first component of its result
  _$k_ : (w â‡’k w') â†’ K w â†’ K w'
  h $k k = h k .fst

  -- restriction of a factorisation function
  -- to the second component of its result
  _$âŠ†_ : (h : w â‡’k w') â†’ (k : K w) â†’ k âŠ†k (h $k k)
  h $âŠ† k = h k .snd

  -- extensional equality for factorising functions
  _â‰‹[â‡’k]_ : w â‡’k w' â†’ w â‡’k w' â†’ Set
  h â‰‹[â‡’k] h' = (k : K _{-w-}) â†’ h k â‰‹[ï¼ ] h' k

  â‰‹[â‡’k]-refl : (h : w â‡’k w') â†’ h â‰‹[â‡’k] h
  â‰‹[â‡’k]-refl h = Î» k â†’ â‰‹[ï¼ ]-refl (h k)

  â‰‹[â‡’k]-sym : {h h' : w â‡’k w'} â†’ h â‰‹[â‡’k] h' â†’ h' â‰‹[â‡’k] h
  â‰‹[â‡’k]-sym p = Î» k â†’ â‰‹[ï¼ ]-sym (p k)

  â‰‹[â‡’k]-trans : {h h' h'' : w â‡’k w'} â†’ h â‰‹[â‡’k] h' â†’ h' â‰‹[â‡’k] h'' â†’ h â‰‹[â‡’k] h''
  â‰‹[â‡’k]-trans p q = Î» k â†’ â‰‹[ï¼ ]-trans (p k) (q k)

  -- (W, â‡’k) forms a category
  â‡’k-refl[_] : âˆ€ w â†’ w â‡’k w
  â‡’k-refl[ w ] = Î» k â†’ k , âŠ†k-refl[ k ]

  â‡’k-trans : w â‡’k w' â†’ w' â‡’k w'' â†’ w â‡’k w''
  â‡’k-trans h h' = Î» k â†’ (h' $k (h $k k)) , âŠ†k-trans (h $âŠ† k) (h' $âŠ† (h $k k))

  â‡’k-trans-unit-left : (h : w â‡’k w') â†’ â‡’k-trans â‡’k-refl[ w ] h â‰‹[â‡’k] h
  â‡’k-trans-unit-left h = Î» k â†’ â‰¡-refl , âŠ†k-trans-unit-left (h $âŠ† k)

  â‡’k-trans-unit-right : (h : w â‡’k w') â†’ â‡’k-trans h â‡’k-refl[ w' ] â‰‹[â‡’k] h
  â‡’k-trans-unit-right h = Î» k â†’ â‰¡-refl , âŠ†k-trans-unit-right (h $âŠ† k)

  â‡’k-trans-assoc : (h : u â‡’k v) (h' : v â‡’k w) (h'' : w â‡’k w')
    â†’ â‡’k-trans (â‡’k-trans h h') h'' â‰‹[â‡’k] â‡’k-trans h (â‡’k-trans h' h'')
  â‡’k-trans-assoc h h' h'' = Î» k
    â†’ â‰¡-refl , âŠ†k-trans-assoc (h $âŠ† k) (h' $âŠ† (h $k k)) (h'' $âŠ† (h' $k (h $k k) ))

  record CFrame : Setâ‚ where

    field

      -- i.e. factor : w âŠ† w' â†’ (k : K w) â†’ Î£ (K w') Î» k' â†’ k âŠ†k k'
      factor : w âŠ† w' â†’ w â‡’k w'

      --
      -- factor is functorial in its first argument
      --
      factor-pres-refl :
          factor âŠ†-refl â‰‹[â‡’k] â‡’k-refl[ w ]
      factor-pres-trans : {w w' : W} (i : w âŠ† w') (i' : w' âŠ† w'')
        â†’ factor (âŠ†-trans i i') â‰‹[â‡’k] â‡’k-trans (factor i) (factor i')

  module _ (CF : CFrame) where

    open CFrame CF

    record Coverage : Setâ‚ where

      field

        -- "Covering family"
        -- Every neighbor in a neighborhood is reachable via âŠ†
        cfamily        : (k : K w) â†’ ForAllW k (w âŠ†_)

      strFam : {k : K w} (i : v âŠ† v') â†’ ForAllW k (v' âŠ†_) â†’ ForAllW k (v âŠ†_)
      strFam i fam x = âŠ†-trans i (fam x)

      wkFam : {k : K w} {k' : K w'} â†’ k âŠ†k k' â†’ ForAllW k (w âŠ†_) â†’ ForAllW k' (w âŠ†_)
      wkFam is fam x = let (_ , x' , i) = is x in âŠ†-trans (fam x') i

      field
        -- factorisation square commutes
        family-stable : (i : w âŠ† w') (k : K w)
          â†’ ForAllWâ‰‹ _ (wkFam (factor i $âŠ† k) (cfamily k)) (strFam i (cfamily (factor i $k k)))

    record Pointed : Set where

      field

        -- a "pointed" neighborhood
        pointK[_]     : âˆ€ w â†’ K w

        -- w is a member of pointK[ w ]
        pointâˆˆ[_]     : âˆ€ w â†’ w âˆˆ pointK[ w ]

        -- every neighbor in pointK is an intuitionistic future of w reachable through âŠ†
        pointK-family : ForAllW (pointK[ w ]) (w âŠ†_ )

        -- coherence condition on pointed neighborhoods
        -- i.e. reaching w (as its own neighbor) via pointK-family must be through âŠ†-refl
        pointK-coh[_] : âˆ€ w â†’ pointK-family pointâˆˆ[ w ] â‰¡ âŠ†-refl[ w ]

      pointK-pres-âŠ† : w âŠ† w' â†’ pointK[ w ] âŠ†k pointK[ w' ]
      pointK-pres-âŠ† {w} {w'} i = Î» x â†’ w , pointâˆˆ[ w ] , âŠ†-trans i (pointK-family x)

      -- canonical factorisation of pointK[ w ] at w'
      pointï¼ [_] : w âŠ† w' â†’ pointK[ w ] ï¼  w'
      pointï¼ [ i ] = pointK[ _ ] , pointK-pres-âŠ† i

      field
        -- factor preserves "identity" in its second argument
        factor-pres-point : (i : w âŠ† w') â†’ factor i pointK[ w ] â‰‹[ï¼ ] pointï¼ [ i ]
